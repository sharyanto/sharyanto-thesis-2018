#!/usr/bin/env perl

# Before running this script, you have to already run the import-* scripts.

use 5.010001;
use strict;
use warnings;

use DBIx::Connect::MySQL;
use Progress::Any '$progress';
use Progress::Any::Output 'TermProgressBarColor';

my $dbh = DBIx::Connect::MySQL->connect(
    "dbi:mysql:database=mtgox", undef, undef, {RaiseError=>1});

my $stat_columns = <<_;
  First_Trade_Stamp DATETIME,
  Last_Trade_Stamp  DATETIME,
  Num_Trades INT NOT NULL,
  Num_Sells  INT NOT NULL,
  Num_Buys   INT NOT NULL,
  Bitcoin_Volume       DOUBLE NOT NULL,
  Bitcoin_Volume_Sells DOUBLE NOT NULL,
  Bitcoin_Volume_Buys  DOUBLE NOT NULL
_

$dbh->do("
CREATE TABLE IF NOT EXISTS _Trader_User_Stats (
  _rowid INT NOT NULL PRIMARY KEY AUTO_INCREMENT,
  User__ CHAR(36) NOT NULL, UNIQUE(User__),
  $stat_columns
) ENGINE='MyISAM';
");
$dbh->do("
CREATE TABLE IF NOT EXISTS _Trader_Index_Stats (
  _rowid INT NOT NULL PRIMARY KEY AUTO_INCREMENT,
  `Index` INT NOT NULL, UNIQUE(`Index`),
  $stat_columns
) ENGINE='MyISAM';
");

my ($num_recs) = $dbh->selectrow_array("SELECT COUNT(*) FROM Trade WHERE `Index` NOT IN (SELECT `Index` FROM _Index_Exclude) ORDER BY Stamp");
#my $num_recs = 100;
$progress->target($num_recs);

my $sth = $dbh->prepare("SELECT * FROM Trade WHERE `Index` NOT IN (SELECT `Index` FROM _Index_Exclude) ORDER BY Stamp");
$sth->execute;

my %Trader_By_Index;
my %Trader_By_User__;
my $i = 0;
while (my $row = $sth->fetchrow_hashref) {
    $i++;
    $progress->update(message => "Processing row #$i");
    my @hashinfo;
    if ($row->{Index}) {
        push @hashinfo, [\%Trader_By_Index, $row->{Index}];
    }
    if ($row->{User__}) {
        push @hashinfo, [\%Trader_By_User__, $row->{User__}];
    }

    for my $hashinfo (@hashinfo) {
        my $key = $hashinfo->[1];
        $hashinfo->[0]{$key} //= {
            First_Trade_Stamp => $row->{Stamp},
            Num_Trades => 0,
            Num_Buys   => 0,
            Num_Sells  => 0,
            Bitcoin_Volume       => 0,
            Bitcoin_Volume_Sells => 0,
            Bitcoin_Volume_Buys  => 0,
        };
        my $hash = $hashinfo->[0]{$key};

        $hash->{Last_Trade_Stamp} = $row->{Stamp};
        $hash->{Num_Trades}++;
        $hash->{Num_Buys}++  if $row->{Type} eq 'buy';
        $hash->{Num_Sells}++ if $row->{Type} eq 'sell';
        $hash->{Bitcoin_Volume}       += $row->{Bitcoins};
        $hash->{Bitcoin_Volume_Buys}  += $row->{Bitcoins} if $row->{Type} eq 'buy';
        $hash->{Bitcoin_Volume_Sells} += $row->{Bitcoins} if $row->{Type} eq 'sell';
    }
}

$progress->target( $progress->target + keys(%Trader_By_Index));
$sth = $dbh->prepare("INSERT INTO _Trader_Index_Stats (`Index`, First_Trade_Stamp,Last_Trade_Stamp, Num_Trades,Num_Sells,Num_Buys, Bitcoin_Volume,Bitcoin_Volume_Sells,Bitcoin_Volume_Buys) VALUES (?, ?,?, ?,?,?, ?,?,?)");
for my $index (sort keys %Trader_By_Index) {
    $progress->update(message => "Inserting stat for Trader Index #$index");
    my $hash = $Trader_By_Index{$index};
    #use DD; dd $hash;
    $sth->execute(
        $index,
        $hash->{First_Trade_Stamp}, $hash->{Last_Trade_Stamp},
        $hash->{Num_Trades}, $hash->{Num_Sells}, $hash->{Num_Buys},
        $hash->{Bitcoin_Volume}, $hash->{Bitcoin_Volume_Sells}, $hash->{Bitcoin_Volume_Buys},
    );
}

$progress->target( $progress->target + keys(%Trader_By_User__));
$sth = $dbh->prepare("INSERT INTO _Trader_User_Stats (User__, First_Trade_Stamp,Last_Trade_Stamp, Num_Trades,Num_Sells,Num_Buys, Bitcoin_Volume,Bitcoin_Volume_Sells,Bitcoin_Volume_Buys) VALUES (?, ?,?, ?,?,?, ?,?,?)");
for my $user (sort keys %Trader_By_User__) {
    $progress->update(message => "Inserting stat for Trader User__ #$user");
    my $hash = $Trader_By_User__{$user};
    $sth->execute(
        $user,
        $hash->{First_Trade_Stamp}, $hash->{Last_Trade_Stamp},
        $hash->{Num_Trades}, $hash->{Num_Sells}, $hash->{Num_Buys},
        $hash->{Bitcoin_Volume}, $hash->{Bitcoin_Volume_Sells}, $hash->{Bitcoin_Volume_Buys},
    );
}

$progress->finish;
