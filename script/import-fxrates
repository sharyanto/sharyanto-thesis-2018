#!/usr/bin/env perl

# Before running this script, please do the following:
#
# 1. Install the required modules from CPAN.
#
# 2. Create a MySQL database called `mtgox` and create a ~/.my.cnf configuration
#    file that contains access information to the database, e.g.:
#
#    [client]
#    user=root
#    password=your_db_password
#
# 3. Run gen-fxrates-csv script.

use 5.010001;
use strict;
use warnings;
use FindBin '$Bin';

use DBIx::Connect::MySQL;
use POSIX qw(strftime);

my $dbh = DBIx::Connect::MySQL->connect(
    "dbi:mysql:database=mtgox", undef, undef, {RaiseError=>1});

$dbh->do("
CREATE TABLE IF NOT EXISTS Fxrate (
  _rowid INT NOT NULL PRIMARY KEY AUTO_INCREMENT,
  Stamp DATETIME NOT NULL, -- in UTC
  Currency1 CHAR(3) NOT NULL,
  Currency2 CHAR(3) NOT NULL,
  UNIQUE (Stamp, Currency1, Currency2),
  Price DOUBLE NOT NULL
) ENGINE='MyISAM';
");

my @files = glob("$Bin/../data/fxrates/*-USD.csv");
@files or die "Please run gen-fxrates-csv script first\n";

my $sth = $dbh->prepare("
INSERT INTO Fxrate (Stamp, Currency1, Currency2, Price) VALUES (?,?,?,?)
");

for my $file (@files) {
    warn "Processing '$file' ...\n";
    my ($currency1) = $file =~ /([A-Z][A-Z][A-Z])-USD/;
    open my $fh, "<", $file or die;

    my $i = 0;
    while (<$fh>) {
        chomp;
        my @F = split/,/,$_;
        for (@F) { s/^"//; s/"$// }
        $F[0] = strftime "%Y-%m-%d %H:%M:%S", gmtime $F[0];
        $sth->execute($F[0], $currency1, 'USD', $F[1]);
        $sth->execute($F[0], 'USD', $currency1, 1/$F[1]);
    }
}
